{ config, lib, pkgs, ... }:
let
  name = "ari-nixtcloud";
in
{
  imports =
    [ ./nextcloud.nix
      ./first-boot.nix
    ];

  networking.hostName = name;
  
  #### You can define your wireless network here if you don't want to use ethernet cable.
  #networking.wireless.enable = true;  # Enable wireless support via wpa_supplicant.
  #networking.wireless.networks = { SSID = { psk = "pass"; };  };

  ### Static IP address
  networking = {
    useDHCP = false;
    interfaces.end0 = {
      useDHCP = false;
      ipv4.addresses = [{
        address = "192.168.1.100";  
        prefixLength = 24;
      }];
    };
    defaultGateway = "192.168.1.1";
    nameservers = [ "192.168.1.1" "8.8.8.8" "1.1.1.1" ];
  };

  # Set your time zone.
  time.timeZone = "Asia/Bangkok";
  
  ########## Most probably you don't need and don't want to change the nix settings below #########
  nix.settings = {
	  experimental-features = "nix-command flakes";
	  auto-optimise-store = false; #fewer writes to sd-card
    substituters = [ "https://nix-community.cachix.org" ];
	  trusted-public-keys = [ "nix-community.cachix.org-1:mB9FSh9qf2dCimDSUo8Zy7bkq5CX+/rkCWyvRCYg3Fs=" ];
    require-sigs = false;
  };
  nix.gc = {
	  automatic = true;
	  dates = "weekly";
	  options = "--delete-older-than 5d";
  };
  ##########################################################################################

  ######################################## Size reduction options ########################################
  programs.command-not-found.enable = false;
  i18n.supportedLocales = lib.mkForce [ "en_US.UTF-8/UTF-8" ];
  environment.defaultPackages = lib.mkForce [];
  environment.stub-ld.enable = false;
  boot.supportedFilesystems = lib.mkForce [ "vfat" "ext4" "exfat" "ntfs3" ];
  systemd = {
    coredump.enable = false;
    enableEmergencyMode = false;
  };
  security.audit.enable = false;
  security.auditd.enable = false;
  boot.plymouth.enable = false;
  zramSwap.enable = false;
  documentation = {
    enable = false;
    man.enable = false;
    info.enable = false;
    doc.enable = false;
    nixos.enable = false;
  };
  services.logrotate.enable = false;
  services.udisks2.enable = false;
  xdg = {
    autostart.enable = false;
    icons.enable = false;
    mime.enable = false;
    sounds.enable = false;
  };
  #######################################################################################################

  ### DO NOT CHANGE the username. After the system is installed, you can change the password with 'passwd' command.
   users.users.admin = {
     isNormalUser = true;
     extraGroups = [ "wheel" ]; # Enable ‘sudo’ for the user.
     initialPassword = "admin";
   };
  
  ### If you know what the following line does, you can uncomment it ;)
  #security.sudo.wheelNeedsPassword = false;

  ###### Packages that are available systemwide. Most probably you don't need to change this. ######
  environment.systemPackages = [
      pkgs.curl
      pkgs.jq
      pkgs.htop
      pkgs.avahi
      pkgs.nssmdns
  ];  

  ### Optional daily reboot and periodic nextcloud maintenance
  ### Weekly check and apply of updates
  services.cron.enable = true;
  services.cron.systemCronJobs = [
    #"0 2 * * *    root    /run/current-system/sw/bin/reboot"
    "0 2 * * *    root    sudo -u nextcloud /run/current-system/sw/bin/nextcloud-occ maintenance:repair"
    "5 2 * * 0    root    sudo -u nextcloud /run/current-system/sw/bin/nextcloud-occ maintenance:mimetype:update-db"
    "10 2 * * 0   root    sudo -u nextcloud /run/current-system/sw/bin/nextcloud-occ maintenance:mimetype:update-js"
    "0 3 * * 0    root    /run/current-system/sw/bin/bash /etc/nixos/updater.sh"
    #"0 15 * * 5   root    /run/current-system/sw/bin/bash /etc/nixos/backup-reminder.sh" #will be added in the future
  ];
  
  ########## SSH & Security ##########
  services.openssh.enable = true;
  services.openssh.settings.PermitRootLogin = "no";
  services.openssh.settings.PasswordAuthentication = "false";
  users.users.admin.openssh.authorizedKeys.keys = [ "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAIPYMsWNLPImIoLmOdtfS3Dw92/0PE1jTp6M/uTr9L2SI natthapongxch67@gmail.com" ];
  networking.firewall = {
    enable = true;
    allowedTCPPorts = [ 22 80 443 ];
    ## We add the following to firewall so Nixtcloud can be accessed with Holesail not only remotely, but also from the local network
    extraCommands = ''
      # Allow connections from common home network IP ranges
      iptables -A nixos-fw -s 192.168.0.0/16 -j nixos-fw-accept
    '';
  };  
  #####################################
  
  #### DON'T CHANGE ANYTHING BELOW THIS LINE UNLESS YOU ABSOLUTELY KNOW WHAT YOU ARE DOING ###

  ########## AVAHI ########## 
  services.avahi = {
    enable = true;
    hostName = name;
    nssmdns4 = true; 
    reflector = true;
    openFirewall = true;
    publish.enable = true;
    publish.userServices = true;
    publish.domain = true;
    publish.addresses = true;
  };
  ###########################

  ###### System services ######

  #### This service initializes the system and checks stuff after each reboot. ####
  systemd.services.startup = {
    description = "Startup";
    wantedBy = [ "multi-user.target" ];
    after = ["network.target" "nextcloud-setup.service"];
    enable = true;
    path = [ pkgs.coreutils pkgs.qrencode pkgs.openssl ];
    script = ''
          /run/current-system/sw/bin/nextcloud-occ app:enable files_external
          /run/current-system/sw/bin/nextcloud-occ app:disable files_trashbin
          /run/current-system/sw/bin/nextcloud-occ config:app:set preview jpeg_quality --value="55"
          /run/current-system/sw/bin/nextcloud-occ app:disable nextbackup      
          if [ ! -f /var/lib/nextcloud/data/admin/files/rebooter.txt ]; then
              touch /var/lib/nextcloud/data/admin/files/rebooter.txt
              chown nextcloud:nextcloud /var/lib/nextcloud/data/admin/files/rebooter.txt
              /run/current-system/sw/bin/nextcloud-occ files:scan --path=/admin/files
          fi
          if [ ! -f /var/lib/nextcloud/data/admin/files/remote.txt ]; then
              touch /var/lib/nextcloud/data/admin/files/remote.txt
              echo -n "hs://s000$(openssl rand -hex 32)" > /var/lib/nextcloud/data/admin/files/remote.txt
              qrencode -o /var/lib/nextcloud/data/admin/files/remote.jpg -r /var/lib/nextcloud/data/admin/files/remote.txt -s 10
              chown nextcloud:nextcloud /var/lib/nextcloud/data/admin/files/remote.txt
              chown nextcloud:nextcloud /var/lib/nextcloud/data/admin/files/remote.jpg
              /run/current-system/sw/bin/nextcloud-occ files:scan --path=/admin/files
          fi
          if [ ! -f /mnt/Public/public.txt ]; then
              touch /mnt/Public/public.txt
              echo -n "hs://s000$(openssl rand -hex 32)" > /mnt/Public/public.txt
              qrencode -o /mnt/Public/public.jpg -r /mnt/Public/public.txt -s 10
              chown -R nextcloud:nextcloud /mnt/Public
          fi
    '';
    serviceConfig.Type = "oneshot";
    before = ["mymnt.service" "p2pmagic.service" "p2public.service" "rebooter.service"];
    onSuccess = ["mymnt.service" "p2pmagic.service" "p2public.service" "rebooter.service"];
  };  
  ############################################################################

  ### The following service automounts external usb devices with correct permissions and creates the corresponding Nextcloud external storages.######
  systemd.services.mymnt = {
    enable = true;
    path = [ pkgs.util-linux pkgs.gawk pkgs.exfatprogs ];
    serviceConfig = {
		  Type = "simple";
		  ExecStart = "${pkgs.bash}/bin/bash /etc/nixos/mounter.sh";
		  Restart = "always";
		  RestartSec = "30";
      KillMode = "process";
	  };
  };
  ################################################################################
  
  #### The following service enables Holesail to do its magic ####
  services.holesail-server.p2pmagic = {
  	enable = true;
    host = "localhost";
  	port = 8080;
  	key-file = "/var/lib/nextcloud/data/admin/files/remote.txt";
    user = "nextcloud";
    group = "nextcloud";
  };
  ###############################################################################
  
  ### The following service enables the share of the Public folder with Holesail ###
  services.holesail-filemanager.p2public = {
  	enable = true;
    host = "localhost";
  	key-file = "/mnt/Public/public.txt";
    directory = "/mnt/Public";
    username = "test";
    password = "test";
    role = "user";
    user = "nextcloud";
    group = "nextcloud";
  };
  ##############################################################################
  
  ##### This service reboots the system if the rebooter.txt file gets deleted. On startup, it gets created again ####   
  systemd.services.rebooter = {
    description = "rebooter";
    enable = true;
    path = [  ];
    script = ''
          if [ ! -f /var/lib/nextcloud/data/admin/files/rebooter.txt ]; then
            reboot
          fi
    '';
    serviceConfig.Type = "simple";
    serviceConfig.Restart = "always";
    serviceConfig.RestartSec = "30";
    after = ["startup.service"];
  };
  ##############################################################################
  
  ###### Defining the mounter script. This script mounts the external usb devices with correct permissions. ######
  environment.etc."nixos/mounter.sh" = { 
    source = ./mounter.sh;
    mode = "0744";
    group = "wheel";
  };

  environment.etc."nixos/updater.sh" = {
    source = ./updater.sh;
    mode = "0744";
    group = "wheel";
  };
  ##############################################################################################################

}


